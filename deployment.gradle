
def packageDesc = 'Utility class for interrupting native operations in native code'
def packageSite = 'https://github.com/chdir/fdutil'
def mainLic = 'Apache-2.0'
def libVcs = 'https://github.com/chdir/fdutil'
def ver = '0.3.1'

def props = new Properties()

def propFile
def propName = project.properties.deploymentConfig
if(propName && (propFile = file(propName)).exists()) {
    new FileInputStream(propFile).withStream { InputStream is ->
        props.load(is)
    }
}

if (props) {
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    def pomXmlContents = new XmlParser().parseText """
        <dependencies>
            <dependency>
                <groupId>net.sf.xfd</groupId>
                <artifactId>common</artifactId>
                <version>0.3</version>
                <scope>compile</scope>
                <optional>false</optional>
            </dependency>
        </dependencies>
    """

    publishing {
        publications {
            xfdbase(MavenPublication) {
                artifact ([
                        source: file("$buildDir/outputs/aar/lib-release.aar"),
                        extension: 'aar',
                ])
                artifact androidSourcesJar
                groupId 'net.sf.xfd'
                artifactId 'i10n'
                version ver

                pom.withXml { provider ->
                    provider.asNode().append(pomXmlContents)
                }
            }
        }
    }

    bintray {
        user = props['bintrayUser']
        key = props['bintrayKey']
        publications = ['xfdbase']

        pkg {
            repo = 'maven'
            name = 'xfd-interruption'
            desc = packageDesc
            websiteUrl = packageSite
            issueTrackerUrl = 'https://github.com/chdir/fdutil/issues'
            vcsUrl = libVcs
            licenses = [mainLic]
            labels = ['android']
            publicDownloadNumbers = true

            githubRepo = 'chdir/fdutil'

            version {
                name = ver
            }

            publish = false
        }
    }

    tasks.bintrayUpload.dependsOn tasks.assemble
}